
Неофициальный код:  
https://github.com/Kris-fynn/VB.NET/blob/main/OutputAndVariables/Form1.vb

Плэйлист:  
https://www.youtube.com/playlist?list=PLTd6ceoshpremSFQxYM4L0p4Q6hR-4e4J  
https://www.youtube.com/watch?v=XM3mXeTrSeE&list=PLTd6ceoshpremSFQxYM4L0p4Q6hR-4e4J&index=3

Chapters:  

Visual Basic.NET Programming. Beginner Lesson 1. Hello Visual Studio  
Visual Basic.NET Programming. Beginner Lesson 2. Customise The Visual Studio IDE  
Visual Basic.NET Programming. Beginner Lesson 3. Output and Variables  
Visual Basic.NET Programming. Beginner Lesson 4. Variable Data Types  
Visual Basic.NET Programming. Beginner Lesson 5. Input with Windows Forms  
Visual Basic.NET Programming. Beginner Lesson 6. Debugging Code  
Visual Basic.NET Programming. Beginner Lesson 7. Arithmetic Operators  
Visual Basic.NET Programming. Beginner Lesson 8. Complex Arithmetic Expressions  
Visual Basic.NET Programming. Beginner Lesson 9. Selection with If Statements  
Visual Basic.NET Programming. Beginner Lesson 10. Logical and Relational Operators 1  
Visual Basic.NET Programming. Beginner Lesson 11. Logical and Relational Operators 2  
Visual Basic.NET Programming. Beginner Lesson 12. Select Case  
Visual Basic.NET Programming. Beginner Lesson 13. For Next  
Visual Basic.NET Programming. Beginner Lesson 14. Practice For Next Loops & If Blocks  
Visual Basic.NET Programming. Beginner Lesson 15. Do While  
Visual Basic.NET Programming. Beginner Lesson 16. Condition Controlled Loops  
Visual Basic.NET Programming. Beginner Lesson 17. Array Variables  
Visual Basic.NET Programming. Beginner Lesson 18. Practice Arrays & Loops  
Visual Basic.NET Programming. Beginner Lesson 19. Linear Search  
Visual Basic.NET Programming. Beginner Lesson 20. Two Dimensional Arrays  
Visual Basic.NET Programming. Beginner Lesson 21. 2D Arrays & Nested Loops  


## 1. Hello VS

> Создание проекта

В окне приветствия:

-> Create a new project  
-> Выбрать язык в выпадающем списке - Visual Basic  
-> Выбрать прототип - Windows Forms App (.NET Framework)  
-> Next  
-> Выбрать Имя, папку, оставить галочку о совпадении проекта и решения (solution), оставить .NET Framework 4.7.2  
-> Create  


> Код для кнопки

```vb
Public Class Form1

    Private Sub btnStart_Click(sender As Object, e As EventArgs) Handles btnStart.Click

        MsgBox("hello world")
        MsgBox("how are you")
        MsgBox("i hope you are well")

    End Sub

End Class
```

## 2. Customise VS

>    WindowsApp1.sln

Это файл Решений (Solution) проекта в папке проекта. По нему можно запустить проект.

Некоторые настройки VS:

Место проектов меняется в

> Tools - Options - Projects and Solutions - Locations

## 3. Output and Variables

3 фундаментальные конструкции существуют в процедурных языках:  
- последовательность
- выбор
- итерация

Конкатенация:

    &

Варианты написания msgBox:
```vb
MsgBox("welcome to the course")

MessageBox.Show("this is another message")
```

Пример объявления и использования переменных:

```vb
Dim stFirstName As String
Dim stLastName As String

stFirstName = "Kevin"
stLastName = "Drumm"

MsgBox("hello and welcome " & stFirstName & " " & stLastName & " I hope you are well")

stFirstName = "Murvin"
stLastName = "Drake"

MsgBox("hello and welcome " & stFirstName & " " & stLastName & " I hope you are well")
```

Dim - образовано от dimension (выделение памяти).

## 4. Variable Data Types

```vb
 Private Sub btnDataTypes_Click(sender As Object, e As EventArgs) Handles btnDataTypes.Click

     Dim stMake As String
     Dim stModel As String
     Dim iDoors As Integer
     Dim stColour As String
     Dim bTaxed As Boolean
     Dim iEngineSize As Integer
     Dim decPrice As Decimal
     Dim dtDateRegistered As Date

     stMake = "Ford"
     stModel = "Escort"
     iDoors = 5
     stColour = "red"
     bTaxed = True
     iEngineSize = 1200
     decPrice = 999.99
     dtDateRegistered = #11/2/2020#  ' Это 2 ноября, американский формат

     MsgBox("the car is " & stMake & " " & stModel & " " &
            iDoors & " " & stColour & " " & bTaxed & " " &
            iEngineSize & " " & decPrice & " " & dtDateRegistered)

 End Sub
```

Пробелы " " можно заменить константой vbNewLine, которая переносит строки. 

```c#
    # вместо
    & " " &
    # будет
    & vbNewLine &
```

## 5. Input with Windows Forms

Простая процедура для формы, после нажатия на кнопку появится поле ввода:

```vb
    Private Sub btnGo_Click(sender As Object, e As EventArgs) Handles btnGo.Click

        Dim stFirstName As String

        stFirstName = InputBox("please enter your first name")

        MsgBox("hello " & stFirstName)

    End Sub
```

Процедура для формы с тремя полями ввода и кнопкой:

```vb
Private Sub btnGo_Click(sender As Object, e As EventArgs) Handles btnGo.Click

    Dim stFirstName As String
    Dim stLastName As String
    Dim stGender As String

    stFirstName = txtFirstName.Text   'Это значение берётся из текстового поля с названием txtFirstName
    stLastName = txtLastName.Text
    stGender = txtGender.Text

    MsgBox("hello " & stFirstName & " " & stLastName & " you are a " & stGender)

End Sub
```

То же самое, но со списком выбора, List Box

Код кнопки Go
```vb
Private Sub btnGo_Click(sender As Object, e As EventArgs) Handles btnGo.Click
    
    Dim stFirstName As String
    Dim stLastName As String
    Dim stGender As String
    Dim stOccupation As String

    stFirstName = txtFirstName.Text
    stLastName = txtLastName.Text
    stGender = txtGender.Text

    stOccupation = lstOccupation.SelectedItem  'Это значение берётся из поля списка с названием lstOccupation

    MsgBox("hello " & stFirstName & " " & stLastName & " you are a " & stGender & " " & stOccupation)

End Sub
```

То же самое, но с добавлением в список выбора еще трех элементов через загрузку формы (своего рода конструктор при инициализации формы). Список будет добавлен в выбор опций, когда форма загрузится.  


Код формы
```vb
Private Sub Form1_Load(sender As Object, e As EventArgs) Handles MyBase.Load

    'Эти значения будут автоматически добавлены
    'в поле списка при загрузке формы
    lstOccupation.Items.Add("Writer") 
    lstOccupation.Items.Add("Actor")
    lstOccupation.Items.Add("Painter")

End Sub
```

## 6. Debugging Code

### Breakpoints

Установка точки останова  

<img src="img/breakpoint-set.jpg" alt="drawing" width="600"/>

Кнопки управления:  

<img src="img/breakpoint-buttons.jpg" alt="drawing" width="600"/>

Различные окна, такие как `Output`, доступны в меню `Debug -> Windows`. Некоторые из них, такие как `Locals` (локальные переменные), могут стать доступны после выполения некоторых точек останова.

### Syntax Error  

Выявление ошибок после запуска  

<img src="img/syntax-error.jpg" alt="drawing" width="600"/>

## 7. Arithmetic Operators

```vb
Private Sub btnCalculate_Click(...)...

    Dim iNumber1 As Integer
    Dim iNumber2 As Integer

    Dim iResult As Integer

    iNumber1 = txtNumber1.Text
    iNumber2 = txtNumber2.Text

    iResult = iNumber1 / iNumber2 'при Integer выдаст целое число 
    MsgBox(iResult)

End Sub
```

```vb
Private Sub btnCalculate_Click(...)...

    Dim dblNumber1 As Double
    Dim dblNumber2 As Double

    Dim dblResult As Double

    dblNumber1 = txtNumber1.Text
    dblNumber2 = txtNumber2.Text

    dblResult = dblNumber1 + dblNumber2
    MsgBox(dblResult)

    dblResult = dblNumber1 - dblNumber2
    MsgBox(dblResult)

    dblResult = dblNumber1 * dblNumber2
    MsgBox(dblResult)

    dblResult = dblNumber1 / dblNumber2
    MsgBox(dblResult)

    dblResult = dblNumber1 ^ dblNumber2  'возведение в степень
    MsgBox(dblResult)

    dblResult = dblNumber1 \ dblNumber2  'целочисленное деление
    MsgBox(dblResult)

    dblResult = dblNumber1 Mod dblNumber2  'остаток от деления
    MsgBox(dblResult)
End Sub
```

При арифметических операциях из текстовых полей VB преобразует вводимые числа из строкового типа в числовой.  

## 8. Complex Arithmetic Expressions

```vb
 Private Sub btnCalculate_Click(sender As Object, e As EventArgs) Handles btnCalculate.Click
     Dim decPrice As Decimal
     Dim iQuantity As Integer
     Dim decDiscount As Decimal
     Dim decTotalCost As Decimal
     Dim decPostage As Decimal

     decPrice = 5
     iQuantity = 10
     decDiscount = 2
     decPostage = 3

     decTotalCost = ((decPrice - decDiscount) * iQuantity) + decPostage

     MsgBox(decTotalCost)

     Dim iResult As Integer
     iResult = 10 - 5 + 2
     MsgBox(iResult)

     iResult = 10 + 2 - 5
     MsgBox(iResult)

     iResult = 10 - 2 + 5
     MsgBox(iResult)


     iResult = 10 / 5 * 2
     MsgBox(iResult)

     iResult = 2 * 10 / 5
     MsgBox(iResult)

     iResult = 10 * 5 / 2
     MsgBox(iResult)

 End Sub
```

## 9. Selection with If Statements

3 фундаментальные конструкции существуют в процедурных языках:  
- последовательность
- выбор
- итерация

Здесь рассказывается о Выборе.

В VB одиночное равно (=) используется и для присваивания, и для проверки на равенство.

Однострочный if

```vb
 Dim stCountry As String
 stCountry = txtCountry.Text

 If stCountry = "Australia" Then MsgBox("G'day mate")
```

Многострочный if

```vb
 Private Sub btnGreet_Click(sender As Object, e As EventArgs) Handles btnGreet.Click
     Dim stCountry As String
     stCountry = txtCountry.Text

     If stCountry = "Australia" Then
         MsgBox("G'day mate")
         MsgBox("Good on ya")
         MsgBox("No worries")
     End If
 End Sub
```

ElseIf

```vb
Private Sub btnGreet_Click(sender As Object, e As EventArgs) Handles btnGreet.Click
    Dim stCountry As String
    stCountry = txtCountry.Text

    If stCountry = "Australia" Then
        MsgBox("G'day mate")
        MsgBox("Good on ya")
        MsgBox("No worries")
    ElseIf stCountry = "France" Then
        MsgBox("Bonjour")
        MsgBox("Comment allez-vous?")
    End If
End Sub
```

Несколько ElseIf и один Else

```vb
Private Sub btnGreet_Click(sender As Object, e As EventArgs) Handles btnGreet.Click
    Dim stCountry As String
    stCountry = txtCountry.Text

    If stCountry = "Australia" Then
        MsgBox("G'day mate")
        MsgBox("Good on ya")
        MsgBox("No worries")
    ElseIf stCountry = "France" Then
        MsgBox("Bonjour")
        MsgBox("Comment allez-vous?")
    ElseIf stCountry = "Japan" Then
        MsgBox("Konnichiwa")
        MsgBox("Choushi wa dou desu ka")
    Else
        MsgBox("Hello there")
        MsgBox("I hope, you are well")
    End If
End Sub
```

If чувствительны к регистру.  
Можно убрать чувствительность к регистру тем, что в переменную всегда будут попадать преобразованные буквы в верхнем регистре.  

```vb
 Dim stCountry As String
 stCountry = txtCountry.Text
 stCountry = stCountry.ToUpper
 'введённые буквы будут преобразованы в верхний регистр

 If stCountry = "AUSTRALIA" Then
     MsgBox("G'day mate")
     MsgBox("Good on ya")
     MsgBox("No worries")
 ElseIf stCountry = "FRANCE" Then
     MsgBox("Bonjour")
     MsgBox("Comment allez-vous?")
 ElseIf stCountry = "JAPAN" Then
     MsgBox("Konnichiwa")
     MsgBox("Choushi wa dou desu ka")
 Else
     MsgBox("Hello there")
     MsgBox("I hope, you are well")
 End If
```

Вариант - не перезаписывать введённую пользователем переменную, т.к. она может понадобиться далее в коде, а преобразовывать её только для If тестов.  

```vb
 Dim stCountry As String
 stCountry = txtCountry.Text

 If stCountry.ToUpper = "AUSTRALIA" Then
 ' частное преобразование
     MsgBox("G'day mate")
     MsgBox("Good on ya")
     MsgBox("No worries")
 ElseIf stCountry.ToUpper = "FRANCE" Then
 ' частное преобразование
     MsgBox("Bonjour")
     MsgBox("Comment allez-vous?")
 ElseIf stCountry.ToUpper = "JAPAN" Then
 ' частное преобразование
     MsgBox("Konnichiwa")
     MsgBox("Choushi wa dou desu ka")
 Else
     MsgBox("Hello there")
     MsgBox("I hope, you are well")
 End If

 MsgBox("You entered " & stCountry)
 ' изначально введённая пользователем переменная
```

## 10. Logical and Relational Operators 1

```vb
Private Sub btnGrade_Click(sender As Object, e As EventArgs) Handles btnGrade.Click

    Dim iScore As Integer
    iScore = txtExamScore.Text

    If iScore < 0 Or iScore > 100 Then
        MsgBox("That is not a valid score. Enter a number between 0 and 100")
        Exit Sub ' Останавливает программу здесь
    End If

    MsgBox("all done")

End Sub
```

```vb
    Dim iScore As Integer
    iScore = txtExamScore.Text

    If iScore < 0 Or iScore > 100 Then
        MsgBox("That is not a valid score. Enter a number between 0 and 100")
        Exit Sub ' Останавливает программу здесь
    End If

    If iScore >= 50 Then
        MsgBox("pass")
    End If

    If iScore < 50 Then
        MsgBox("fail")
    End If

    MsgBox("all done")
```
Однако в этом варианте программа проходит все блоки If, даже если какие-то из них избыточны, если нужное значение уже найдено. Поэтому лучше использовать блоки с If .. ElseIf .  

```vb
Dim iScore As Integer
iScore = txtExamScore.Text

If iScore < 0 Or iScore > 100 Then
    MsgBox("That is not a valid score. Enter a number between 0 and 100")
ElseIf iScore >= 50 Then
    MsgBox("pass")
ElseIf iScore < 50 Then
    MsgBox("fail")
End If

MsgBox("all done")
```

Проверка на невалидные данные, текст вместо числа.

```vb
 Dim iScore As Integer

 ' Если при приведении к числу, всё хорошо, то продолжаем присвоение.
 ' IsNumeric не преобразует в число, а спрашивает, можно ли преобразовать в число.
 If IsNumeric(txtExamScore.Text) = True Then
    ' и дополнительно вводим приведение типа,
    ' преобразовать в целое число,
    ' т.к. по умолчанию из поля ввода даже цифры приходят как текст
    iScore = CInt(txtExamScore.Text)
 Else
     ' Иначе остановим программу
     MsgBox("You must enter a number")
     Exit Sub
 End If

 If iScore < 0 Or iScore > 100 Then
     MsgBox("That is not a valid score. Enter a number between 0 and 100")
 ElseIf iScore >= 50 Then
     MsgBox("pass")
 ElseIf iScore < 50 Then
     MsgBox("fail")
 End If

 MsgBox("all done")
```

## 11. Logical and Relational Operators 2

```vb
Private Sub btnGrade_Click(sender As Object, e As EventArgs) Handles btnGrade.Click

    Dim iScore As Integer

    ' Если при приведении к числу, всё хорошо, то продолжаем присвоение.
    ' IsNumeric не преобразует в число, а спрашивает, можно ли преобразовать в число.
    If IsNumeric(txtExamScore.Text) = True Then
        ' и дополнительно вводим приведение типа,
        ' преобразовать в целое число,
        ' т.к. по умолчанию из поля ввода даже цифры приходят как текст
        iScore = CInt(txtExamScore.Text)
    Else
        ' Иначе остановим программу
        MsgBox("You must enter a number")
        Exit Sub
    End If

    ' Использование Not и And 
    If Not (iScore >= 0 And iScore <= 100) Then
        MsgBox("That is not a valid score. Enter a number between 0 and 100")
    ElseIf iScore <= 20 Then
        MsgBox("GRADE F")
    ElseIf iScore > 20 And iScore <= 30 Then
        MsgBox("GRADE E")
    ElseIf iScore > 30 And iScore <= 40 Then
        MsgBox("GRADE D")
    ElseIf iScore > 40 And iScore <= 50 Then
        MsgBox("GRADE C")
    ElseIf iScore > 50 And iScore <= 70 Then
        MsgBox("GRADE B")
    Else
        MsgBox("GRADE A")
    End If

    MsgBox("all done")

End Sub
```

## 12 Select Case

```vb
    Private Sub btnCheck_Click(sender As Object, e As EventArgs) Handles btnCheck.Click
        Dim iTemperature As Integer
        ' присвоение и преобразование типа в integer
        iTemperature = CInt(txtTemperature.Text)

        Dim iWindSpeed As Integer
        iWindSpeed = 30

        'If может проверить несколько переменных за раз

        'If iTemperature <= 0 And iWindSpeed > 20 Then
        '    MsgBox("That will feel really cold")
        'End If

        'Case проверяет только одну переменную за раз
        Select Case iTemperature
            Case Is = 0
                MsgBox("Freezing")
                MsgBox("Water will freeze")
                MsgBox("and you can go skating")
            Case Is < 0
                MsgBox("Sub zero")
                'If можно вкладывать в конструкцию Case
                If iWindSpeed > 20 Then
                    MsgBox("That will feel really cold")
                End If
            Case 1 To 10    '1 и 10 включительно
                MsgBox("Cold")
            Case 11, 12, 13, 14, 15, 16, 17, 18, 19, 20
                MsgBox("Warm")
            Case Else
                MsgBox("Hot")
        End Select

    End Sub
```

## 13. For Next

Простые циклы

```vb
    Dim iCount As Integer

    ' Цикл будет повторяться 5 раз
    For iCount = 1 To 5
        MsgBox("hello")
        MsgBox("how are you")
        MsgBox("well I hope")
    Next

    For iCount = 1 To 5
        MsgBox("hello " & iCount)
    Next

    ' Издать звуковой сигнал Windows
    For iCount = 1 To 5
        Beep()
        ' Пауза 3 секунды
        Threading.Thread.Sleep(3000)
    Next
```

Цикл с шагом выполнения 5 

```vb
    Dim iCount As Integer

    ' Цикл будет повторяться 50 раз, но с шагом выполнения 5
    For iCount = 0 To 50 Step 5
        MsgBox(iCount)
    Next
```

Цикл со счётом в обратном порядке 

```vb
    Dim iCount As Integer

    ' Цикл будет повторяться 50 раз в обратном порядке, но с шагом выполнения 5
    For iCount = 50 To 0 Step -5
        MsgBox(iCount)
    Next
```

Цикл со сборным сообщением в одном окне

```vb
    ' Вывести все сообщения в одном окне
    For iCount = 50 To 0 Step -5
        ' Сбор всех данных в одну строку в цикле,
        ' приращение на подобие var.=
        stOut = stOut & iCount & vbNewLine
    Next

    MsgBox(stOut)
```

## 14. Practice For Next Loops & If Blocks

```vb
 ' максимальное число счёта
 Dim iMax As Integer
 ' нечетные или четные числа считать
 Dim stoddorEven As String
 ' счётчик в циклах
 Dim x As Integer

iMax = InputBox("What number do you want to count up to?")
stoddorEven = InputBox("Do you want odd numbers or even numbers?")

If stOddOrEven = "even" Then
    ' если четное, считать от 2 по 2
    For x = 2 To iMax Step 2
        MsgBox(x)
    Next
ElseIf stOddOrEven = "odd" Then
    ' если нечетное, то считать от 1 по 2
    For x = 1 To iMax Step 2
        MsgBox(x)
    Next
End If
```

## 15. Do While 

Цикл For

```vb
Dim iCount As Integer
' iCount без присвоения числа 
' по умолчанию будет равно нулю

For iCount = 1 To 5
    MsgBox(" Hello " & iCount)
Next
```

Цикл Do While

```vb
Dim iCount As Integer

Do While iCount < 5
    iCount = iCount + 1
    MsgBox(" Hello " & iCount)
Loop
' Вывод до 5 включительно

MsgBox("all done")

```

Еще один вид цикла Do While - Do Until

```vb
Dim iCount As Integer

Do Until iCount = 5
    iCount = iCount + 1
    MsgBox(" Hello " & iCount)
Loop
' Вывод до 5 включительно

MsgBox("all done")

```

Until в конце. Условие выхода находится в конце - код внутри цикла выполнится хотя бы один раз.

```vb
Dim iCount As Integer

Do 
    iCount = iCount + 1
    MsgBox(" Hello " & iCount)
Loop Until iCount = 5
' Вывод до 5 включительно

MsgBox("all done")

```

Еще один вид цикла Do While - когда While в конце цикла.  

```vb
Dim iCount As Integer

Do 
    iCount = iCount + 1
    MsgBox(" Hello " & iCount)
Loop While iCount < 5
' Вывод до 5 включительно

MsgBox("all done")

```

## 16. Condition Controlled Loops   

> Why a Do Loop Is Called a Condition Controlled Loop

Всплывающее окно для ввода имени будет вызываться до тех пор, пока не будет вбито имя `KEVIN`.  
Поэтому `Do-While` называют циклом с условием, а не циклом с подсчётом, как `For-Next`.  

```vb
Dim stName As String

Do While stName <> "KEVIN"
    stName = InputBox("Please enter your name")
Loop

MsgBox("Hello " & stName)
```

> Infinite Loop

Выход из бесконечного цикла с помощью `Exit Do`:

```vb
Dim stName As String

Do While 1 = 1
    stName = InputBox("Please enter your name")
    If stName = "KEVIN" Then
        Exit Do
    End If
Loop

MsgBox("Hello " & stName)
```

> Implicit Type Conversion  
> Explicit Type Conversion (Явное Преобразование типов)

Преобразование типов. Пока не будет возможность преобразовать строку из инпута в число (например, "99" в число в отличие от "hello"), цикл будет снова и снова запрашивать InputBox.  

```vb
Dim stAge As String
Dim iAge As Integer

' Пока stAge не будет преобразовано в число,
' цикл будет вызыватья вновь.
' Другие вариации:
' Do While IsNumeric(stAge) <> True
' Do While Not IsNumeric(stAge)

' IsNumeric проверяет, можно ли будет преобразовать в число
Do While IsNumeric(stAge) = False
    ' InputBox всегда считывает строку, даже если она введена в поле ввода как число
    stAge = InputBox("Please enter your age in years")
    ' Неявное преобразование типа произошло бы при использовании
    ' iAge = InputBox("Please enter your age in years")
    ' т.к. тут используется переменная с типом число,
    ' и VB преобразует в число введённую строку за кулисами, неявно, без нашего указания,
    ' но проблема возникнет, если пользователь вобьёт в строку вместо, например, "59" - "привет", - 
    ' программа вылетит, пытаясь совершить преобразование "привет" в число.
Loop

' Преобразовать в целое число
' Явное Преобразование типов
iAge = CInt(stAge)

MsgBox("Hello, you are " & iAge)

```


## 17. Array Variables

Переменные-массивы

Далее префиксы переменных означают следующее:  
a - array,  
st - string  

```vb
' Объявление массива
Dim astFruits(4) As String

astFruits(0) = "Banana"
astFruits(1) = "Orange"
astFruits(2) = "Pineapple"
astFruits(3) = "Strawberry"
astFruits(4) = "Mango"

MsgBox(astFruits(2))
' Pineapple

```

Вывод элемента через переменную

```vb
' Объявление массива
Dim astFruits(4) As String

astFruits(0) = "Banana"
astFruits(1) = "Orange"
astFruits(2) = "Pineapple"
astFruits(3) = "Strawberry"
astFruits(4) = "Mango"

Dim i As Integer
i = 3

MsgBox(astFruits(i))
' Strawberry

```

Вывод всех элементов через цикл

```vb
' Объявление массива
Dim astFriuts(4) As String

astFruits(0) = "Banana"
astFruits(1) = "Orange"
astFruits(2) = "Pineapple"
astFruits(3) = "Strawberry"
astFruits(4) = "Mango"

Dim i As Integer

' Вывод всех элементов через цикл
For i = 0 To 4
    MsgBox(astFruits(i))
Next

```

## 18. Practice Arrays & Loops

Начало у всех модулей одинаково

```vb
' ai - a массив, i числовой
Dim aiData(4) As Integer

aiData(0) = 5
aiData(1) = 7
aiData(2) = 33
aiData(3) = 22
aiData(4) = 8
```

Далее у каждого модуля своё продолжение

```vb
Public Class Form1
    Private Sub btnExercise1_Click(sender As Object, e As EventArgs) Handles btnExercise1.Click

        ' Output each item in a separate message box one after another
        ' Вывод всех чисел по очереди

        Dim i As Integer

        For i = 0 To 4
            MsgBox(aiData(i))
        Next

    End Sub

    Private Sub btnExercise2_Click(sender As Object, e As EventArgs) Handles btnExercise2.Click

        ' Output all of the items in the same message box on separate lines
        ' Вывод всех чисел за раз


        Dim i As Integer
        Dim stOut As String

        For i = 0 To 4
            ' Здесь в итерациях происходит конкатенация предыдущей строки с новой,
            ' своего рода output=.output (php).
            stOut = stOut & aiData(i) & vbNewLine
        Next

        ' Вывести весь результат за раз
        MsgBox(stOut)

    End Sub

    Private Sub btnExercise3_Click(sender As Object, e As EventArgs) Handles btnExercise3.Click

        ' Add up all of the items then output the total in a message box
        ' Сумма всех чисел

        Dim i As Integer
        Dim iTotal As Integer

        For i = 0 To 4
            iTotal = iTotal + aiData(i)
        Next

        MsgBox("The total is " & iTotal)

    End Sub

    Private Sub btnExercise4_Click(sender As Object, e As EventArgs) Handles btnExercise4.Click

        ' Calculate the average of the items then output it in a message box
        ' Среднее всех чисел

        Dim i As Integer
        Dim iTotal As Integer

        For i = 0 To 4
            iTotal = iTotal + aiData(i)
        Next

        MsgBox("The average is " & iTotal / 5)

    End Sub

    Private Sub btnExercise5_Click(sender As Object, e As EventArgs) Handles btnExercise5.Click

        ' Add up the items greater than 20 then output the result in a message box
        ' Сложить все элементы больше 20

        Dim i As Integer
        Dim iTotal As Integer

        For i = 0 To 4
            If aiData(i) > 20 Then
                iTotal = iTotal + aiData(i)
            End If
        Next

        MsgBox("The total of the items bigger than 20 is " & iTotal)

    End Sub

    Private Sub btnExercise6_Click(sender As Object, e As EventArgs) Handles btnExercise6.Click

        ' Find the largest item then output it in a message box
        ' Найти наибольшее значение в массиве

        Dim i As Integer
        Dim iMax As Integer

        iMax = 0

        ' На каждой итерации сравниваем значение iMax с текущим из массива,
        ' и если из массива больше,
        ' то присваиваем его к iMax
        For i = 0 To 4
            If aiData(i) > iMax Then
                iMax = aiData(i)
            End If
        Next

        MsgBox("The largest item is " & iMax)

    End Sub

    Private Sub btnExercise7_Click(sender As Object, e As EventArgs) Handles btnExercise7.Click

        ' Find the smallest item then output it in a message box
        ' Найти наименьшее значение в массиве

        Dim i As Integer
        Dim iMin As Integer

        iMin = 10000

        ' На каждой итерации сравниваем значение iMin с текущим из массива,
        ' и если из массива меньше,
        ' то присваиваем его к iMin
        For i = 0 To 4
            If aiData(i) < iMin Then
                iMin = aiData(i)
            End If
        Next

        MsgBox("The smallest item is " & iMin)

    End Sub

    Private Sub btnExercise8_Click(sender As Object, e As EventArgs) Handles btnExercise8.Click

        ' Replace each item in the array with a new value that is twice as big then output the new values
        ' Вывести за раз все числа массива, каждое из которых помножено на 2

        Dim i As Integer

        ' Удваиваем все значения массива
        For i = 0 To 4
            aiData(i) = aiData(i) * 2
        Next

        Dim stOut As String

        ' Выводим все значения за раз
        For i = 0 To 4
            stOut = stOut & aiData(i) & vbNewLine
        Next

        MsgBox(stOut)

    End Sub

End Class

```

## 19. Linear Search

Линейный поиск - поиск при переборе значений, которые могут поступать из массива, БД, текстового файла, Excel-файла, веб-страницы.  

```vb

Public Class Form1
    Private Sub btnLinearSearch_Click(sender As Object, e As EventArgs) Handles btnLinearSearch. Click

    Dim stTarget As String
    Dim astFruit(9) As String
    Dim bFound As Boolean

    astFruit(0) = "Apple"
    astFruit(1) = "Mango"
    astFruit(2) = "Strawberry"
    astFruit(3) = "Banana"
    astFruit(4) = "Tomato"
    astFruit(5) = "Grape"
    astFruit(6) = "Pear"
    astFruit(7) = "Lemon" 
    astFruit(8) = "Melon"
    astFruit(9) = "Fig"

    ' Окно с полем поиска
    stTarget = InputBox("Which fruit are you looking for?")
    
    Dim i As Integer
    
    For i = 0 To 9
        ' Варианты приведения к одному регистру
        ' If UCase(astFruit(i)) = UCase(stTarget) Then
        ' If LCase(astFruit(i)) = LCase(stTarget) Then
        If astFruit(i).ToUpper = stTarget.ToUpper Then
            bFound = True
            ' Выйти из цикла полностью, 
            ' как только будет найдено истинное значение
            Exit For
        End If
    Next
    
    ' Вывести результат поиска на экран
    If bFound = True Then
        MsgBox("found it")
    Else    
        MsgBox("not found")
    End If

End Sub

```

## 20

```vb

```

```vb

```

```vb

```



